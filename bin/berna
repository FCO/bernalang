#!/usr/bin/env perl6
use Berna::Parser;
use Berna::Compiler;
use Berna::RunTime;
subset Code of Str;

my %*SUB-MAIN-OPTS = :named-anywhere;

multi MAIN(*@file, Str :$o, *%_) {
    my $run-time-class = Berna::MachineCode with $o;
    my $code = @file>>.IO>>.slurp.join("\n");
    run $code, :$o, |(:run-time-class($_) with $run-time-class), |%_
}

multi MAIN(Str :$e!, Str :$o, *%_) {
    my $run-time-class = Berna::MachineCode with $o;
    run $e, :$o, |(:run-time-class($_) with $run-time-class), |%_
}

multi run(
    Str $code,
    Bool :$print-match      = False,
    Bool :$print-code       = False,
    Bool :$print-ast        = False,
    Bool :$print-list       = False,
    Bool :$print-runtime    = False,
         :$run-time-class   = Berna::RunTime,
    Str  :$o,
) {
    my $parser   = Berna::Parser.new: :debug($print-match);
    my $compiler = Berna::Compiler.new;

    if $print-code {
        note "CODE:";
        note $code;
        note "------------\n"
    }
    my $ast = $parser.parse: $code;
    if $print-ast {
        note "AST:";
        note $ast>>.gist.join: "\n";
        note "------------\n"
    }
    die "unknown error" without $ast;
    my @list = $compiler.compile: $ast;
    if $print-list {
        note "LIST:";
        note $++, ": \t", $_ for @list;
        note "------------\n"
    }

    my $run-time = $run-time-class.new: :code(@list), :debug($print-runtime), :vars($parser.actions.vars), :$o;
    $run-time.run
}
